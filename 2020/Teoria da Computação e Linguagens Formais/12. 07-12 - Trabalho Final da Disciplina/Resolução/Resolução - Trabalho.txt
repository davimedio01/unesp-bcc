;---------------------------------------------------------------------------------------------------
; Nome: Davi Augusto Neves Leite
; RA: 191027383
;
; Data de Entrega: 27/12/2020
; Trabalho da Disciplina de Teoria da Computação e Linguagens Formais
;
; Projetar uma Máquina de Turing (MT) para executar a seguinte equação:	
; 	F(x,y,z) = {x*y+z 	se x <= z
;		   {x-z		se x > z
;		   {x, y, z > 0 e inteiros
;
; Considere: "1" para os dígitos e "C" como caractere separador
;---------------------------------------------------------------------------------------------------
;---------------------------------------------------------------------------------------------------
; O código será separado em três etapas principais: a primeira de verificação de x e z, 
; a segunda que performa x*y+z, e a terceira que perfoma x-z.
;---------------------------------------------------------------------------------------------------
;---------------------------------------------------------------------------------------------------
; Primeira Etapa: verificando se x <= z ou não (x > z)
;---------------------------------------------------------------------------------------------------
;---------------------------------------------------------------------------------------------------

0 * * * q0 ;Inicial

q0 1 X r q1
q1 1 1 r q1
q1 C C r q2
q2 1 1 r q2
q2 C C r q3
q3 X X r q3
q3 1 X l q4
q4 X X l q4
q4 C C l q5
q5 1 1 l q5
q5 C C l q6
q6 1 1 l q6
q6 X X r q0

q0 C C r q7  ;Caso seja X <= Z: segunda etapa
q3 _ _ l q19 ;Caso seja X > Z: terceira etapa

;---------------------------------------------------------------------------------------------------
;---------------------------------------------------------------------------------------------------
; Segunda Etapa: caso x <= z, realizar a operação: x * y + z
;---------------------------------------------------------------------------------------------------
;---------------------------------------------------------------------------------------------------

q7 C C r q7
q7 X X r q7
q7 1 1 r q7
q7 _ _ l q8
q8 C C l q8
q8 1 1 l q8
q8 X 1 l q8
q8 _ _ r q9

;Início da operação de x*y + z
q9 1 _ r q10
q10 1 1 r q10
q10 C C r q11
q11 1 X r q12

q12 1 1 r q12
q12 C C r q13
q13 1 1 r q13
q13 _ 1 l q14
q14 C C l q14
q14 1 1 l q14
q14 X X r q11

;Basicamente, remarca todo o Y e volta para o X para reiniciar o processo
q11 C C l q15
q15 X 1 l q15
q15 C C l q16
q16 1 1 L q16
q16 _ _ r q9	;Looping

q9 C _ r q17
q17 1 _ r q17
q17 C _ r q18_accepted

q18_accepted * * * halt	;Estado final

;---------------------------------------------------------------------------------------------------
;---------------------------------------------------------------------------------------------------
; Terceira Etapa: caso x > z, realizar a operação: x - z
;---------------------------------------------------------------------------------------------------
;---------------------------------------------------------------------------------------------------

q19 1 1 l q19
q19 C C l q19
q19 X 1 l q19
q19 _ _ r q20

;Início da operação X - Z
q20 1 1 r q20
q20 C C r q21
q21 1 1 r q21
q21 C C r q22
q22 X X r q22
q22 1 X l q23

q23 X X l q23
q23 C C l q24
q24 1 1 l q24
q24 C C l q25
q25 1 1 l q25
q25 _ _ r q26
q26 1 _ r q20	;Looping

;Saída do looping e resultado da subtração
q22 _ _ l q27
q27 X _ l q27
q27 C _ l q28
q28 1 _ l q28
q28 C _ l q29
q29 1 1 l q29
q29 _ _ r q30_accepted

q30_accepted * * * halt	;Estado final
;---------------------------------------------------------------------------------------------------
;---------------------------------------------------------------------------------------------------
